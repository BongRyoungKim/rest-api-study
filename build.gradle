apply plugin: "java"
apply plugin: "war"
apply plugin: "eclipse"
apply plugin: "eclipse-wtp"
apply plugin: "idea"

version = '0.1.0'
sourceCompatibility = '1.7'
compileJava.options.encoding = "UTF-8"

/**
 * 기본실행 테스크
 */
defaultTasks 'initProject'

/**
 *  repository Configuration
 *  ref : http://www.gradle.org/docs/current/userguide/artifact_dependencies_tutorial.html
 */
repositories {
    mavenCentral()
}

configurations {
    querydslapt
    moreLibs
}

eclipse {
    classpath {
        downloadSources = true
    }
}

// Plugin idea(IntelliJ)용 모듈설정
idea {
    module {
        downloadJavadoc = true
        downloadSources = true
    }
}

//version
def versions = [
        spring: "3.2.2.RELEASE",
        hibernate: "4.3.0.Final",
        springDataJpa: "1.2.0.RELEASE",
        springSecurity: "3.1.4.RELEASE",
        slf4j: "1.6.6",
        logback: "1.0.6",
        thymeleaf: "2.0.16",
        lombok: "0.12.0",
        boneCP: "0.7.1.RELEASE",
        googleGuava: "13.0.1",
        modelmapper: "0.5.6",
        querydsl: "3.2.1",
        jackson: "1.9.9",
        h2database: "1.3.172",
        jodaTime: "2.1"
]

def paths = [
        querydsl: "src/querydsl",
        mainSourceBase: "src/main/resources/base",
        mainSourceLocal: "src/main/resources/local",
        mainSourceDev: "src/main/resources/dev"
]

def hibernate = [
        "org.hibernate:hibernate-core:${versions.hibernate}",
        "org.hibernate:hibernate-validator:${versions.hibernate}",
        "org.hibernate:hibernate-entitymanager:${versions.hibernate}",
        "org.hibernate.javax.persistence:hibernate-jpa-2.1-api:1.0.1.Final"
]

/**
 * Maven의 Profiles 를 Gradle 에서 사용하기!
 * ex) gradle -Pprofile=local or dev
 * 참고 : http://wiki.kwonnam.pe.kr/gradle/from_maven?s[]=gradle&s[]=profile
 */
final String DEFAULT_PROFILE = 'local'
allprojects {
    if (!project.hasProperty('profile') || !profile) {
        ext.profile = DEFAULT_PROFILE
    }
    println ">> Use profile : ${profile}"

    // 리소스에 각 프로필별 리소스 디렉토리 추가
    sourceSets {
        main {
            resources {
                //기본설정값과 개발환경에 대한 설정값을 가진 디렉토리 이용
                srcDirs = ["src/main/resource/base","src/main/resources/${profile}"]
            }
        }
        querydsl {
            java {
                srcDir "/src/querydsl"
            }
        }
    }
}


//의존성 라이브러리 목록
dependencies {
    //annotation
    compile "javax.inject:javax.inject:1"

    compile "org.springframework:spring-core:${versions.spring}"
    compile "org.springframework:spring-context:${versions.spring}"
    compile "org.springframework:spring-context-support:${versions.spring}"
    compile "org.springframework:spring-webmvc:${versions.spring}"
    compile "org.springframework:spring-aop:${versions.spring}"
    compile "org.springframework:spring-tx:${versions.spring}"
    compile "org.springframework:spring-aspects:${versions.spring}"
    compile "org.springframework:spring-jdbc:${versions.spring}"
    compile "org.springframework:spring-orm:${versions.spring}"
    compile "org.springframework:spring-web:${versions.spring}"
    compile "org.springframework:spring-webmvc:${versions.spring}"
    compile "org.springframework:spring-test:${versions.spring}"

    //spring-data-jpa
    compile("org.springframework.data:spring-data-jpa:${versions.springDataJpa}") {
        exclude group: "org.aspectj"
    }

    //Spring Security
    compile "org.springframework.security:spring-security-core:${versions.springSecurity}"

    //QueryDsl : http://www.querydsl.com/
    compile "com.mysema.querydsl:querydsl-apt:${versions.querydsl}"
    compile "com.mysema.querydsl:querydsl-jpa:${versions.querydsl}"
    querydslapt "com.mysema.querydsl:querydsl-apt:${versions.querydsl}"

    //compile  : http://www.thymeleaf.org/
    compile "org.thymeleaf:thymeleaf:${versions.thymeleaf}"
    compile "org.thymeleaf:thymeleaf-spring3:${versions.thymeleaf}"

    compile "org.apache.commons:commons-lang3:3.1"

    //BoneCP : http://jolbox.com/
    compile "com.jolbox:bonecp:${versions.boneCP}"

    compile "org.slf4j:slf4j-api:${versions.slf4j}"
    runtime "org.slf4j:jcl-over-slf4j:${versions.slf4j}"
    runtime "org.slf4j:log4j-over-slf4j:${versions.slf4j}"

    compile "ch.qos.logback:logback-core:${versions.logback}"
    compile "ch.qos.logback:logback-classic:${versions.logback}"

    // Google Guava(Collection Utils) : http://code.google.com/p/guava-libraries/
    compile "com.google.guava:guava:${versions.googleGuava}"
    // Joda Time(Java date and time API) : http://joda-time.sourceforge.net/
    compile "joda-time:joda-time:${versions.jodaTime}"

    compile "org.codehaus.jackson:jackson-core-asl:${versions.jackson}"
    compile "org.codehaus.jackson:jackson-mapper-asl:${versions.jackson}"
    // FasterXML/jackson-module-hibernate : https://github.com/FasterXML/jackson-module-hibernate
    compile "com.fasterxml.jackson.datatype:jackson-datatype-hibernate4:2.2.2"

    compile "com.h2database:h2:${versions.h2database}"
    compile "mysql:mysql-connector-java:5.1.25"

    compile "javax.servlet:jstl:1.2"

    compile "org.projectlombok:lombok:${versions.lombok}"
    //ModelMapper : http://modelmapper.org/
    compile "org.modelmapper:modelmapper:${versions.modelmapper}"

    hibernate.collect {
        compile(it) {
            exclude(group: "cglib", module: "cglib")
        }
    }

    // logback(slf4j)를 사용하기 때문에 모든 의존성에서 commons-logging는 제외
    [configurations.runtime, configurations.default]*.exclude(module: 'commons-logging')

    // JAVA 컴파일시 인코딩 설정
    [compileJava, compileTestJava]*.options*.encoding = 'UTF-8'

    testCompile group: "junit", name: "junit", version: "4.+"

    providedCompile 'javax.servlet:javax.servlet-api:3.0.1'

    runtime "javax.servlet:jstl:1.2"
}


//-- QueryDSL
task generateQueryDSL(type: JavaCompile, group: 'build', description: 'Generates the QueryDSL query types') {
    source = sourceSets.main.java
    classpath = configurations.compile + configurations.querydslapt
    options.compilerArgs = [
            "-proc:only",
            "-processor", "com.mysema.query.apt.jpa.JPAAnnotationProcessor"
    ]
    destinationDir = sourceSets.querydsl.java.srcDirs.iterator().next()
    dependencyCacheDir = compileJava.dependencyCacheDir
}
compileJava.dependsOn generateQueryDSL
//-- QueryDSL

task initProject(description: "Initialization Project") << {
    createDir = {
        println "Create Dir : $it"
        it.mkdirs();
    }

    sourceSets.main.resources.srcDirs = ["${paths.mainSourceBase}", "${paths.mainSourceLocal}", "${paths.mainSourceDev}"]
    sourceSets*.java.srcDirs*.each createDir
    sourceSets*.resources.srcDirs*.each createDir
    sourceSets.querydsl.java.srcDir createDir
    createDir webAppDir
    createDir new File(webAppDir, "/WEB-INF");

    processResources {
        from(sourceSets.main.resources.srcDirs) {
        }
    }
}

tasks.eclipse.dependsOn cleanEclipse
tasks.eclipse.dependsOn initProject

compileJava {
    dependsOn generateQueryDSL
    source generateQueryDSL.destinationDir
}

clean {
    delete sourceSets.querydsl.java.srcDirs
}

gradle.afterProject {project, projectState ->
    if (projectState.failure) {
        println "Evaluation of $project FAILED"
    } else {
        println "Evaluation of $project succeeded"
    }
}